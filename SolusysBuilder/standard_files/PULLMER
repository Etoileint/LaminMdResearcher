#!/bin/csh

# Generated by CHARMM-GUI (http://www.charmm-gui.org) v3.7
# Modified by ★Magichoco★

# 确保所有步骤按顺序执行，自动续跑未完成步骤

# Gmx mdp preparation ★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★
set prod_tension = 1
set prod_relaxation = 0

# 设置拉伸阶段参数
set tension_target_force        = 900      # pN
set tension_force_rate          = 2       # pN/ns
set tension_stages              = 300       # 阶段数
set tension_total_time          = 450       # ns

# 设置松弛阶段参数
set relaxation_initial_force    = 400      # pN
set relaxation_force_rate       = 1       # pN/ns
set relaxation_stages           = 100       # 阶段数
set relaxation_total_time       = 400       # ns

# 检查逻辑
set tension_required_time = `echo "$tension_target_force / $tension_force_rate" | bc -l`
if ( `echo "$tension_total_time < $tension_required_time" | bc` ) then
    echo "错误：拉伸总时间 ${tension_total_time} ns 小于力变化所需时间 ${tension_required_time} ns"
    exit 1
endif

set relaxation_required_time = `echo "$relaxation_initial_force / $relaxation_force_rate" | bc -l`
if ( `echo "$relaxation_total_time < $relaxation_required_time" | bc` ) then
    echo "错误：松弛总时间 ${relaxation_total_time} ns 小于力变化所需时间 ${relaxation_required_time} ns"
    exit 1
endif

# 调用生成脚本
if (${prod_tension} == 1) then
    bash TensionMdpGenerator $tension_target_force $tension_force_rate $tension_stages $tension_total_time
    echo "拉伸阶段用mdp文件生成完成"
endif

if (${prod_relaxation} == 1) then
    bash RelaxationMdpGenerator $relaxation_initial_force $relaxation_force_rate $relaxation_stages $relaxation_total_time
    echo "松弛阶段用mdp文件生成完成"
endif
# Gmx mdrun ★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★★
set init = step3_input
set mini_prefix = step4.0_minimization
set equi_prefix = step4.1_equilibration
set prod1_prefix = step5_tension
set prod2_prefix = step6_relaxation
set prod1_keywrd = tension
set prod2_keywrd = relaxation
set prod1_step   = step5
set prod2_step   = step6
set traj = ( zero.xtc )

# ==================== 功能函数定义 ====================
alias check_file 'if (-e \!*) echo "检测到文件 \!* 存在"; if (! -e \!*) echo "未找到文件 \!*"'

# Minimization ☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
echo "\n====== 正在检查能量最小化步骤 ======"
check_file ${mini_prefix}.cplchk

if (! -e ${mini_prefix}.cplchk) then
    echo "启动能量最小化..."
    if (! -e ${mini_prefix}.tpr) then
        gmx grompp -f ${mini_prefix}.mdp -o ${mini_prefix}.tpr -c ${init}.gro -r ${init}.gro -p topol.top -n index.ndx -maxwarn 1 || exit 1
    endif
    
    if (-e ${mini_prefix}.cpt) then
        echo "检测到中断的cpt文件，尝试续跑..."
        gmx mdrun -v -deffnm ${mini_prefix} -cpi ${mini_prefix}.cpt || exit 1
    else
        gmx mdrun -v -deffnm ${mini_prefix} || exit 1
    endif
    date > ${mini_prefix}.cplchk
    echo "生成完成标识文件 ${mini_prefix}.cplchk"
else
    echo "能量最小化已完成，跳过"
endif

# Equilibration ☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
echo "\n====== 正在检查预平衡步骤 ======"
check_file ${equi_prefix}.cplchk

if (! -e ${equi_prefix}.cplchk) then
    if (! -e ${mini_prefix}.cplchk) then
        echo "错误：能量最小化未完成，无法进行预平衡"
        exit 1
    endif

    echo "启动预平衡..."
    if (! -e ${equi_prefix}.tpr) then
        gmx grompp -f ${equi_prefix}.mdp -o ${equi_prefix}.tpr -c ${mini_prefix}.gro -r ${init}.gro -p topol.top -n index.ndx || exit 1
    endif
    
    if (-e ${equi_prefix}.cpt) then
        echo "检测到中断的cpt文件，尝试续跑..."
        gmx mdrun -v -deffnm ${equi_prefix} -cpi ${equi_prefix}.cpt || exit 1
    else
        gmx mdrun -v -deffnm ${equi_prefix} || exit 1
    endif
    date > ${equi_prefix}.cplchk
    echo "生成完成标识文件 ${equi_prefix}.cplchk"
else
    echo "预平衡已完成，跳过"
endif

# Production_tension ☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
if (${prod_tension} == 1) then
    echo "\n====== 正在处理拉伸生产阶段 ======"
    set cnt_t = 1
    while ($cnt_t <= $tension_stages)
        set istep = ${prod1_step}_${cnt_t}
        set crmdp = ${prod1_step}.${cnt_t}_${prod1_keywrd}
        check_file ${istep}.cplchk

        if (-e ${istep}.cplchk) then
            set traj = ( $traj ${istep}.xtc )
            echo "${istep} 已完成，跳过"
            @ cnt_t++
            continue
        endif

        if ($cnt_t == 1 && ! -e ${equi_prefix}.cplchk) then
            echo "错误：预平衡未完成，无法开始生产"
            exit 1
        endif

        if (! -e ${istep}.tpr) then
            echo "生成 ${istep}.tpr ..."
            if ($cnt_t == 1) then
                gmx grompp -f ${crmdp}.mdp -o ${istep}.tpr -c ${equi_prefix}.gro -p topol.top -n index.ndx || exit 1
            else
                @ prev_cnt = $cnt_t - 1
                set prev_istep = ${prod1_step}_${prev_cnt}
                if (! -e ${prev_istep}.cplchk) then
                    echo "错误：前置步骤 ${prev_istep} 未完成"
                    exit 1
                endif
                gmx grompp -f ${crmdp}.mdp -o ${istep}.tpr -c ${prev_istep}.gro -t ${prev_istep}.cpt -p topol.top -n index.ndx || exit 1
            endif
        endif

        echo "启动步骤 ${istep} ..."
        if (-e ${istep}.cpt) then
            echo "检测到检查点文件，尝试续跑..."
            gmx mdrun -v -deffnm ${istep} -cpi ${istep}.cpt -pf ${istep}_pullf.xvg -px ${istep}_pullx.xvg || exit 1
        else
            gmx mdrun -v -deffnm ${istep} -pf ${istep}_pullf.xvg -px ${istep}_pullx.xvg || exit 1
        endif

        set traj = ( $traj ${istep}.xtc )
        date > ${istep}.cplchk
        echo "生成完成标识文件 ${istep}.cplchk"

        # 删除前一步的.gro（从第三步开始且阶段数>2）
        if ($cnt_t >= 3 && $tension_stages > 2) then
            @ prev_cnt = $cnt_t - 1
            set prev_istep = ${prod1_step}_${prev_cnt}
            if (-e ${prev_istep}.gro) then
                echo "删除前一步的.gro文件: ${prev_istep}.gro"
                rm -f ${prev_istep}.gro
            endif
        endif

        @ cnt_t++
    end
endif
# Production_relaxation ☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
if (${prod_relaxation} == 1) then
    echo "\n====== 正在处理松弛生产阶段 ======"
    set cnt_r = 1
    while ($cnt_r <= $relaxation_stages)
        set istep = ${prod2_step}_${cnt_r}
        set crmdp = ${prod2_step}.${cnt_r}_${prod2_keywrd}
        check_file ${istep}.cplchk

        if (-e ${istep}.cplchk) then
            set traj = ( $traj ${istep}.xtc )
            echo "${istep} 已完成，跳过"
            @ cnt_r++
            continue
        endif

        if ($cnt_r == 1 && ! -e ${prod1_step}_${tension_stages}.cplchk) then
            echo "错误：拉伸阶段未完成，无法开始松弛"
            exit 1
        endif

        if (! -e ${istep}.tpr) then
            echo "生成 ${istep}.tpr ..."
            if ($cnt_r == 1) then
                gmx grompp -f ${crmdp}.mdp -o ${istep}.tpr -c ${prod1_step}_${tension_stages}.gro -t ${prod1_step}_${tension_stages}.cpt -p topol.top -n index.ndx || exit 1
            else
                @ prev_cnt = $cnt_r - 1
                set prev_istep = ${prod2_step}_${prev_cnt}
                if (! -e ${prev_istep}.cplchk) then
                    echo "错误：前置步骤 ${prev_istep} 未完成"
                    exit 1
                endif
                gmx grompp -f ${crmdp}.mdp -o ${istep}.tpr -c ${prev_istep}.gro -t ${prev_istep}.cpt -p topol.top -n index.ndx || exit 1
            endif
        endif

        echo "启动步骤 ${istep} ..."
        if (-e ${istep}.cpt) then
            echo "检测到检查点文件，尝试续跑..."
            gmx mdrun -v -deffnm ${istep} -cpi ${istep}.cpt -pf ${istep}_pullf.xvg -px ${istep}_pullx.xvg || exit 1
        else
            gmx mdrun -v -deffnm ${istep} -pf ${istep}_pullf.xvg -px ${istep}_pullx.xvg || exit 1
        endif

        set traj = ( $traj ${istep}.xtc )
        date > ${istep}.cplchk
        echo "生成完成标识文件 ${istep}.cplchk"

        # 删除前一步的.gro（从第三步开始且阶段数>2）
        if ($cnt_r >= 3 && $relaxation_stages > 2) then
            @ prev_cnt = $cnt_r - 1
            set prev_istep = ${prod2_step}_${prev_cnt}
            if (-e ${prev_istep}.gro) then
                echo "删除前一步的.gro文件: ${prev_istep}.gro"
                rm -f ${prev_istep}.gro
            endif
        endif

        @ cnt_r++
    end
endif


# Trajectory Merge ☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
set traj_list = ($traj)
shift traj_list  # 移除初始的"zero.xtc"

if ($#traj_list < 1) then
    echo "错误：未找到轨迹文件"
    exit 1
endif

# 处理第一个文件
set first_file = $traj_list[1]
set processed_files = "$first_file"

# 处理后续文件
if ($#traj_list > 1) then
    shift traj_list
    foreach file ($traj_list)
        set base = ${file:r}
        set processed = ${base}_skip1.xtc
        echo "正在处理 $file : 跳过第一帧..."
        
        gmx trjconv -f $file -o $processed -b 1 << END
0
END
        
        set processed_files = "$processed_files $processed"
    end
endif

# 合并处理后的轨迹
echo "\n正在合并轨迹"
gmx trjcat -f $processed_files -o combined_tf.xtc -cat

# 清理临时文件
foreach temp ($processed_files[2-])
    rm -f $temp
end

echo "轨迹合并完成，最终文件：combined_tf.xtc"

gmx trjconv -f combined_tf.xtc -o combined.xtc -timestep 10

# Trajectory Repair ☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆☆
echo "1" | gmx trjconv -s ${prod1_step}_1.tpr -f combined.xtc -o combined_whole.xtc -pbc whole # 修复分子完整性
echo "1" | gmx trjconv -s ${prod1_step}_1.tpr -f combined_whole.xtc -o combined_nojump.xtc -pbc nojump # 消除漂移
echo -e "1\n1" | gmx trjconv -s ${prod1_step}_1.tpr -f combined_nojump.xtc -o combined_final.xtc -center -pbc mol # 中心重置
echo "1" | gmx trjconv -s ${prod1_step}_1.tpr -f combined_final.xtc -o combined_finalfix.xtc -pbc nojump # 再次消除漂移
echo "轨迹修复完成，最终文件：combined_finalfix.xtc"

